%define parse.error verbose
%skeleton "lalr1.cc"
%locations
%token-table
%glr-parser
%lex-param {void *lexer}
%parse-param {void *lexer} {void **root}
%{
#include <stdio.h>
#include <iostream>
#include <string>
#include <stdarg.h>
#include "lexer/Token.hpp"
#include "lexer/Lexer.hpp"
#include "common/Node.hpp"


//void yyerror(yycontrol& some, char const *s);

#define YYSTYPE CNode*

CNode* add_node(const std::string& name, int argc, ...);
void print_tree(CNode* root);
void print_node(CNode* node, int margin);
void pick_up_children(CNode* parent, CNode* bad_parent);
void add_child(CNode* parent, CNode* child);
%}

%code
{
    namespace yy {
    int yylex (YYSTYPE *lvalp, location *llocp, void* lexer);
    void error (const location& loc, const std::string& msg);
    }
}

%token VAR IS
%token ROUTINE END
%token IDENTIFIER TYPE
%token INTEGER REAL BOOLEAN
%token RECORD ARRAY
%token ASSIGNMENT_SIGN RANGE_SIGN
%token WHILE LOOP FOR
%token REVERSE IN
%token IF THEN ELSE
%token AND OR XOR NOT
%token LT_SIGN LET_SIGN GT_SIGN GET_SIGN EQ_SIGN NEQ_SIGN
%token MULT_SIGN DIV_SIGN MOD_SIGN
%token PLUS_SIGN MINUS_SIGN
%token TRUE FALSE REAL_LITERAL INTEGER_LITERAL
%token RETURN
%token L_SQ_BR R_SQ_BR L_BR R_BR
%token COLON DOT COMMA

%start program
%%
program
    : {$$ = nullptr;}
    | program simple_declaration { $$ = add_node("program", 0); pick_up_children($$, $1); add_child($$, $2); *root = $$;}
    | program routine_declaration { $$ = add_node("program", 0); pick_up_children($$, $1); add_child($$, $2); *root = $$;}
    ;

simple_declaration
    : variable_declarartion { $$ = add_node("simple_declarartion", 1, $1); }
    | type_declaration { $$ = add_node("simple_declaration", 1, $1); }
    ;

variable_declarartion
    : VAR IDENTIFIER COLON type variable_expression { $$ = add_node("variable_declarartion", 3, $2, $4, $5);}
    | VAR IDENTIFIER IS expression { $$ = add_node("variable_declarartion_auto", 2, $2, $4);}
    ;

variable_expression
    : {$$ = nullptr;}
    | IS expression { $$ = add_node("variable_expression", 1, $2);}
    ;

type_declaration
    : TYPE IDENTIFIER IS type { $$ = add_node("type_declaration", 2, $2, $4);}
    ;

routine_declaration
    : ROUTINE IDENTIFIER L_BR routine_parameters R_BR routine_return_type IS body END { $$ = add_node("routine_declaration", 4, $2, $4, $6, $8);}
    ;

routine_return_type
    : {$$ = nullptr;}
    | COLON type { $$ = add_node("routine_return_type", 1, $2);}
    ;

routine_parameters
    : {$$ = nullptr;}
    | parameters { $$ = add_node("routine_parameters", 1, $1);}
    ;

parameters
    : parameters COMMA parameter_declaration { $$ = add_node("parameters", 0); pick_up_children($$, $1); add_child($$, $3);}
    | parameter_declaration { $$ = add_node("parameters", 1, $1);}
    ;

//primitive types?
parameter_declaration
    : IDENTIFIER COLON IDENTIFIER { $$ = add_node("parameter_declaration", 2, $1, $3);}
    | IDENTIFIER COLON primitive_type { $$ = add_node("parameter_declaration", 2, $1, $3);}
    ;

type
    : primitive_type { $$ = add_node("type", 1, $1);}
    | array_type { $$ = add_node("type", 1, $1);}
    | record_type { $$ = add_node("type", 1, $1);}
    | IDENTIFIER { $$ = add_node("type", 1, $1);}
    ;

primitive_type
    : INTEGER { $$ = $1;}
    | REAL { $$ = $1;}
    | BOOLEAN { $$ = $1;}
    ;

record_type
    : RECORD variables_declarartion END { $$ = add_node("record_type", 1, $2);}
    ;

variables_declarartion
    : variable_declarartion variables_declarartion { $$ = add_node("variables_declarartion", 1, $1); pick_up_children($$, $2);}
    | {$$ = nullptr;}
    ;

array_type
    : ARRAY L_SQ_BR expression R_SQ_BR type { $$ = add_node("array_type", 2, $3, $5);}
    ;

body
    : {$$ = nullptr;}
    | body simple_declaration { $$ = add_node("body", 0);  pick_up_children($$, $1); add_child($$, $2);}
    | body statement { $$ = add_node("body", 0); pick_up_children($$, $1); add_child($$, $2);}
    ;

statement
    : assignment  { $$ = add_node("statement", 1, $1);}
    | routine_call { $$ = add_node("statement", 1, $1);}
    | while_loop { $$ = add_node("statement", 1, $1);}
    | for_loop { $$ = add_node("statement", 1, $1);}
    | if_statement { $$ = add_node("statement", 1, $1);}
    | return { $$ = add_node("statement", 1, $1);}
    ;

return
    : RETURN return_value { $$ = add_node("return", 1, $2); }
    ;

return_value
    : {$$ = nullptr;}
    | expression { $$ = add_node("return_value", 1, $1);}
    ;

assignment
    : modifiable_primary ASSIGNMENT_SIGN expression { $$ = add_node("assignment", 2, $1, $3);}
    ;

routine_call
    : IDENTIFIER L_BR arguments R_BR { $$ = add_node("routine_call", 2, $1, $3);}
    ;

arguments
    : {$$ = nullptr;}
    | expressions { $$ = add_node("arguments", 1, $1);}
    ;

expressions
    : expressions COMMA expression { $$ = add_node("expressions", 0);  pick_up_children($$, $1); add_child($$, $3);}
    | expression { $$ = add_node("expressions", 1, $1);}
    ;

while_loop
    : WHILE expression LOOP body END { $$ = add_node("while_loop", 2, $2, $4);}
    ;

for_loop
    : FOR IDENTIFIER range LOOP body END { $$ = add_node("for_loop", 3, $2, $3, $5);}
    ;

range
    : IN reverse expression RANGE_SIGN expression { $$ = add_node("range", 3, $2, $3, $5);}
    ;

reverse
    : {$$ = nullptr;}
    | REVERSE { $$ = $1;}
    ;

if_statement
    : IF expression THEN body else_body END { $$ = add_node("if_statement", 3, $2, $4, $5);}
    ;

else_body
    : {$$ = nullptr;}
    | ELSE body { $$ = add_node("else_body", 1, $2);}
    ;

expression
    : expression logic_operation relation { $$ = add_node("expression", 3, $1, $2, $3);}
    | relation { $$ = add_node("expression", 1, $1);}
    ;

logic_operation
    : AND { $$ = $1;}
    | OR { $$ = $1;}
    | XOR { $$ = $1;}
    ;

relation
    : simple { $$ = add_node("relation", 1, $1);}
    | simple compare_sign simple { $$ = add_node("relation", 3, $1, $2, $3);}
    ;

compare_sign
    : LT_SIGN { $$ = $1;}
    | LET_SIGN { $$ = $1;}
    | GT_SIGN { $$ = $1;}
    | GET_SIGN { $$ = $1;}
    | EQ_SIGN { $$ = $1;}
    | NEQ_SIGN { $$ = $1;}
    ;

simple
    : simple mult_sign_f factor { $$ = add_node("simple", 3, $1, $2, $3);}
    | factor { $$ = add_node("simple", 1, $1);}
    ;
// f mean first priority
mult_sign_f
    : MULT_SIGN { $$ = $1;}
    | DIV_SIGN { $$ = $1;}
    | MOD_SIGN { $$ = $1;}
    ;

factor
    : factor mult_sign_s summand { $$ = add_node("factor", 3, $1, $2, $3);}
    | summand { $$ = add_node("factor", 1, $1);}
    | mult_sign_s summand { $$ = add_node("unary_factor", 2, $1, $2);}
    | NOT summand { $$ = add_node("not_factor", 2, $1, $2);}
    ;
// s mean second priority
mult_sign_s
    : PLUS_SIGN { $$ = $1;}
    | MINUS_SIGN { $$ = $1;}
    ;

summand
    : primary { $$ = add_node("summand", 1, $1);}
    | L_BR expression R_BR { $$ = add_node("summand", 1, $2);}
    ;


primary
    : TRUE { $$ = add_node("boolean", 1, $1);}
    | FALSE { $$ = add_node("boolean", 1, $1);}
    | REAL_LITERAL { $$ = add_node("real", 1, $1);}
    | INTEGER_LITERAL { $$ = add_node("integer", 1, $1);}
    | modifiable_primary { $$ = $1;}
    | routine_call { $$ = $1;}
    ;

modifiable_primary
    : IDENTIFIER { $$ = add_node("modifiable_primary", 1, $1);}
    | modifiable_primary L_SQ_BR expression R_SQ_BR { $$ = add_node("modifiable_primary_array", 2, $1, $3);}
    | modifiable_primary DOT modifiable_primary { $$ = add_node("modifiable_primary_field", 2, $1, $3);}
    ;
%%

void clear_node(CNode * node){
	if (node == nullptr) return;

	for (int i = 0; i < node->children.size(); i++) {
        	clear_node(node->children[i]);
        }

	delete node;
}

void yy::parser::error (const location& loc, const std::string& msg){
    clear_node((CNode*)(*root));
    *root = nullptr;
    fflush(stdout);
    printf("\n%*s\n%*s\n", ((Lexer*)lexer)->column, "^", ((Lexer*)lexer)->column, msg.c_str());
}
int yy::yylex (YYSTYPE *lvalp, location *llocp, void* lexer){
    if (lexer == nullptr) return 0;
    Lexer* lexer1 = (Lexer*) lexer;
    Token currentToken = lexer1->next();
    int tokenType = currentToken.class_name;
    if (tokenType == 0) {return 0;}
    *lvalp = new CNode(currentToken.value);
    return tokenType;
}

CNode* add_node(const std::string& name, int argc, ...) {
	va_list argp;
  CNode* newNode = new CNode(name);

  va_start(argp, argc);
  for (int i = 0; i < argc; i++)
        newNode->children.push_back(va_arg(argp, CNode*));
  va_end(argp);

  return newNode;
}

void pick_up_children(CNode* parent, CNode* bad_parent){
	if (parent == nullptr || bad_parent == nullptr) return;
	for (int i = 0; i < bad_parent->children.size(); i++) {
		parent->children.push_back(bad_parent->children[i]);
	}
	delete bad_parent;
}

void add_child(CNode* parent, CNode* child){
    if (parent == nullptr || child == nullptr) return;
    parent->children.push_back(child);
}
